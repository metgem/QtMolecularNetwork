cmake_minimum_required(VERSION 3.1)
cmake_policy(VERSION 3.1)

# Consider changing the project name to something relevant for you.
project(PySide2MolecularNetwork)

set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../cmake/modules ${CMAKE_MODULE_PATH})
find_package(Cairo REQUIRED)

include(FindFreetype)
include_directories(${FREETYPE_INCLUDE_DIRS})

# ================================ General configuration ======================================

# Set CPP standard to C++11 minimum.
set(CMAKE_CXX_STANDARD 11)

# The sample library for which we will create bindings. You can change the name to something
# relevant for your project.
set(sample_library "qtmolecularnetwork")

# The name of the generated bindings module (as imported in Python). You can change the name
# to something relevant for your project.
set(bindings_library "qmn")

# The header file with all the types and functions for which bindings will be generated.
# Usually it simply includes other headers of the library you are creating bindings for.
set(wrapped_header ${CMAKE_SOURCE_DIR}/bindings.h)

# The typesystem xml file which defines the relationships between the C++ types / functions
# and the corresponding Python equivalents.
set(typesystem_file ${CMAKE_SOURCE_DIR}/bindings.xml)

# Specify which C++ files will be generated by shiboken. This includes the module wrapper
# and a '.cpp' file per C++ type. These are needed for generating the module shared
# library.
set(generated_sources   
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/edge_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/graphicsitemlayer_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/networkscene_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/networkstyle_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/defaultstyle_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/node_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/${bindings_library}_module_wrapper.cpp
    ${CMAKE_CURRENT_BINARY_DIR}/${bindings_library}/${bindings_library}_python.h
    )

# ================================== Shiboken detection ======================================
if(NOT PYTHON_EXECUTABLE)
    find_program(PYTHON_EXECUTABLE "python")
endif()
message(STATUS "Using python interpreter: ${PYTHON_EXECUTABLE}")

find_package(Shiboken2 2.0.0 REQUIRED)
find_package(Shiboken2 COMPONENTS libshiboken REQUIRED)
find_package(PySide2 2.0.0 REQUIRED)
find_package(Qt5 COMPONENTS Widgets Core Gui Svg REQUIRED)
find_package(RDKit REQUIRED)
find_package(qtmolecularnetwork)

message(STATUS "Qt include dirs: ${Qt5Widgets_INCLUDE_DIRS}")

get_property(SHIBOKEN_INCLUDE_DIRS TARGET Shiboken2::libshiboken PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
message(STATUS "Shiboken include dirs: ${SHIBOKEN_INCLUDE_DIRS}")
get_property(PYSIDE_INCLUDE_DIR TARGET PySide2::pyside2 PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
message(STATUS "PySide2 include dir: ${PYSIDE_INCLUDE_DIR}")
get_property(QMN_INCLUDE_DIRS TARGET qmn::qtmolecularnetwork PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
message(STATUS "QtMolecularNetwork include dirs: ${QMN_INCLUDE_DIRS}")

add_definitions(${Qt5Widgets_DEFINITIONS})
add_definitions(-DQT_NO_KEYWORDS)

# ====================== Shiboken target for generating binding C++ files  ====================

list(JOIN Qt5Widgets_INCLUDE_DIRS ";-I" qt_includes)
set(qt_includes "-I${qt_includes}")
list(JOIN QMN_INCLUDE_DIRS ";-I" qmn_includes)
set(qmn_includes "-I${qmn_includes}")
set(includes ${qt_includes} ${qmn_includes})

# Set up the options to pass to shiboken.
set(shiboken_options --generator-set=shiboken --enable-parent-ctor-heuristic
    --enable-return-value-heuristic --use-isnull-as-nb_nonzero
    --avoid-protected-hack --enable-pyside-extensions
    ${qt_includes} ${qmn_includes}
    -I${CMAKE_SOURCE_DIR}/src
    -T${PYSIDE_TYPESYSTEMS}
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    )

set(generated_sources_dependencies ${wrapped_header} ${typesystem_file})

# Add custom target to run shiboken to generate the binding cpp files.add_custom_command(OUTPUT ${generated_sources}
add_custom_command(OUTPUT ${generated_sources}
                    COMMAND "shiboken2"
                    ${shiboken_options} ${wrapped_header} ${typesystem_file}
                    DEPENDS ${generated_sources_dependencies}
                    IMPLICIT_DEPENDS CXX ${wrapped_header}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Running generator for ${typesystem_file}.")


# =============================== CMake target - bindings_library =============================

get_property(SHIBOKEN_LIBRARY TARGET Shiboken2::libshiboken PROPERTY IMPORTED_IMPLIB_RELEASE)
get_property(PYSIDE_LIBRARY TARGET PySide2::pyside2 PROPERTY IMPORTED_IMPLIB_RELEASE)
get_property(QMN_LINK_LIBRARIES TARGET qmn::qtmolecularnetwork PROPERTY INTERFACE_LINK_LIBRARIES)
get_property(QMN_LIBRARY TARGET qmn::qtmolecularnetwork PROPERTY IMPORTED_IMPLIB_RELEASE)
message(STATUS "QtMolecularNetwork lib: ${QMN_LIBRARY}")

# Set the cpp files which will be used for the bindings library.
set(${bindings_library}_sources ${generated_sources})

# Define and build the bindings library.
add_library(${bindings_library} MODULE ${${bindings_library}_sources})

# Apply relevant include and link flags.
target_include_directories(${bindings_library} PRIVATE ${PYTHON_INCLUDE_DIRS})
target_include_directories(${bindings_library} PRIVATE ${Qt5Widgets_INCLUDE_DIRS})
target_include_directories(${bindings_library} PRIVATE ${SHIBOKEN_PYTHON_INCLUDE_DIRS})
target_include_directories(${bindings_library} PRIVATE ${SHIBOKEN_INCLUDE_DIRS})
target_include_directories(${bindings_library} PRIVATE ${PYSIDE_INCLUDE_DIR})
target_include_directories(${bindings_library} PRIVATE ${PYSIDE_INCLUDE_DIR}/QtCore/)
target_include_directories(${bindings_library} PRIVATE ${PYSIDE_INCLUDE_DIR}/QtWidgets/)
target_include_directories(${bindings_library} PRIVATE ${PYSIDE_INCLUDE_DIR}/QtGui/)
target_include_directories(${bindings_library} PRIVATE ${QMN_INCLUDE_DIRS})

target_include_directories(${bindings_library} PRIVATE
    $<INSTALL_INTERFACE:include>
)

list(JOIN CMAKE_SYSTEM_PREFIX_PATH "/lib;" LIBRARIES_PATH)
set(LIBRARIES_PATH "${LIBRARIES_PATH}/lib;$ENV{PREFIX}/lib;$ENV{PREFIX}/Library/lib")
message(STATUS "LIBRARIES_PATH: ${LIBRARIES_PATH}")
target_link_directories(${bindings_library} PUBLIC ${LIBRARIES_PATH})

target_link_libraries(${bindings_library} PRIVATE ${PYSIDE_LIBRARY})
target_link_libraries(${bindings_library} PRIVATE ${SHIBOKEN_PYTHON_LIBRARIES})
target_link_libraries(${bindings_library} PRIVATE ${SHIBOKEN_LIBRARY})
target_link_libraries(${bindings_library} PRIVATE ${CAIRO_LIBRARIES})
target_link_libraries(${bindings_library} PRIVATE ${FREETYPE_LIBRARIES})
target_link_libraries(${bindings_library} PRIVATE ${QMN_LINK_LIBRARIES})
target_link_libraries(${bindings_library} PRIVATE ${QMN_LIBRARY})

if (UNIX)
    target_link_libraries(${bindings_library} PUBLIC qtmolecularnetwork PySide2::pyside2)
endif()

# Adjust the name of generated module.
set_property(TARGET ${bindings_library} PROPERTY PREFIX "")
if(WIN32)
    set_property(TARGET ${bindings_library} PROPERTY OUTPUT_NAME
             "${bindings_library}${PYTHON_EXTENSION_SUFFIX}")
    set_property(TARGET ${bindings_library} PROPERTY SUFFIX "${PYTHON_CONFIG_SUFFIX}.pyd")
else()
    set_property(TARGET ${bindings_library} PROPERTY OUTPUT_NAME
             "${bindings_library}${PYTHON_CONFIG_SUFFIX}")
endif()

# Make sure the linker doesn't complain about not finding Python symbols on macOS.
if(APPLE)
  set_target_properties(${bindings_library} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
endif(APPLE)
